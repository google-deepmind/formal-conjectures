/-
Copyright 2025 The Formal Conjectures Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-/

import FormalConjectures.Util.ProblemImports
/-!
# Some conjectures about ranks of elliptic curves over ‚Ñö

## References

* [PPVW2016] Jennifer Park, Bjorn Poonen, John Voight, and Melanie Matchett Wood.
  A heuristic for boundedness of ranks of elliptic curves,
  https://ems.press/journals/jems/articles/16228

* [BS2013] Manjul Bhargava and Arul Shankar. The average size of the 5-Selmer group of
  elliptic curves is 6, and the average rank is less than 1, https://arxiv.org/pdf/1312.7859

* https://en.wikipedia.org/wiki/Rank_of_an_elliptic_curve
-/

/-- A data structure representing isomoprhism classes of elliptic curves over ‚Ñö.
Every elliptic curve over ‚Ñö is isomorphic to one with Weierstrass equation `y¬≤ = x¬≥ + Ax + B`,
and the pair `(A,B)` is unique if it satisfy the `reduced` condition below.
See Section 5.1 in [PPVW2016]. -/
structure RatEllipticCurve : Type where
  A : ‚Ñ§
  B : ‚Ñ§
  reduced (p : ‚Ñï) : p.Prime ‚Üí ¬¨ ((p ^ 4 : ‚Ñ§) ‚à£ A ‚àß (p ^ 6 : ‚Ñ§) ‚à£ B)
  Œî_ne_zero : 4 * A ^ 3 + 27 * B ^ 2 ‚â† 0

open scoped WeierstrassCurve.Affine
open Module (finrank)

/-- The rank of an elliptic curve over a number field is always finite by the Mordell‚ÄìWeil theorem.
Consequently, the rank is always finite, so `finrank ‚Ñ§ E‚üÆK‚üØ = 0` really means that the group of
rational points is torsion, not that it is of infinite rank. -/
@[category research solved, AMS 11 14]
instance {K} [Field K] [NumberField K] (E : WeierstrassCurve K) [E.IsElliptic] :
    Module.Finite ‚Ñ§ E‚üÆK‚üØ := by
  sorry

namespace RatEllipticCurve

/-- Convert the structure `RatEllipticCurve` to a Weierstrass curve. -/
def toWeierstrass (E : RatEllipticCurve) : WeierstrassCurve ‚Ñö :=
  { a‚ÇÅ := 0, a‚ÇÇ := 0, a‚ÇÉ := 0, a‚ÇÑ := E.A, a‚ÇÜ := E.B }

/-- The rank of an elliptic curve over ‚Ñö. -/
noncomputable abbrev rank (E : RatEllipticCurve) : ‚Ñï := finrank ‚Ñ§ E.toWeierstrass‚üÆ‚Ñö‚üØ

open WeierstrassCurve in
instance (E : RatEllipticCurve) : E.toWeierstrass.IsElliptic where
  isUnit := isUnit_iff_ne_zero.mpr <| by
    convert mul_ne_zero (show (-16 : ‚Ñö) ‚â† 0 by norm_num) (Int.cast_ne_zero.mpr E.Œî_ne_zero)
    simp_rw [toWeierstrass, Œî, b‚ÇÇ, b‚ÇÑ, b‚ÇÜ, Int.cast_add, Int.cast_mul, Int.cast_pow]
    ring

/-- The na√Øve height of an elliptic curve over ‚Ñö. -/
def naiveHeight (E : RatEllipticCurve) : ‚Ñï := max (4 * E.A.natAbs ^ 3) (27 * E.B.natAbs ^ 2)

/-- The set of elliptic curves over ‚Ñö with na√Øve height less than or equal to a given height. -/
def heightLE (H : ‚Ñï) : Set RatEllipticCurve := {E : RatEllipticCurve | E.naiveHeight ‚â§ H}

open scoped Topology
open Filter (atTop)

/-- Formula (5.1.1) of [PPVW2016]: The number of elliptic curves over ‚Ñö with na√Øve height at most
`H` is asymptotically `2^(4/3)*3^(-3/2)/Œ∂(10) * H^(5/6)`. -/
@[category graduate, AMS 11 14]
theorem card_heightLE_div_pow_five_div_six_tensto :
    atTop.Tendsto (fun H ‚Ü¶ (heightLE H).ncard / (H : ‚Ñù) ^ (5 / 6 : ‚Ñù))
      (ùìù (2 ^ (4 / 3 : ‚Ñù) * 3 ^ (-3 / 2 : ‚Ñù) / (riemannZeta 10).re)) := by
  sorry

/-- Conjecture by Goldfeld and Katz‚ÄìSarnak: if elliptic curves over ‚Ñö are ordered by their
heights, then 50% of the curves have rank 0 and 50% have rank 1.
See p. 28 of https://people.maths.bris.ac.uk/~matyd/BSD2011/bsd2011-Bhargava.pdf. -/
@[category research open, AMS 11 14]
theorem half_rank_zero_and_half_rank_one (r : ‚Ñï) (hr : r = 0 ‚à® r = 1) :
    atTop.Tendsto
      (fun H ‚Ü¶ ({E ‚àà heightLE H | E.rank = r}.ncard / (heightLE H).ncard : ‚Ñù)) (ùìù (1 / 2)) := by
  sorry

/-- Theorem 3 of [BS2013]:
when elliptic curves over ‚Ñö are ordered by height, their average rank is < .885. -/
@[category research solved, AMS 11 14]
theorem avg_rank_lt_0885 :
    atTop.limsup (fun H ‚Ü¶ ((‚àë·∂† E : heightLE H, E.1.rank) / (heightLE H).ncard : ‚Ñù)) < 0.885 := by
  sorry

/-- Theorem 4 of [BS2013]:
when elliptic curves over ‚Ñö are ordered by height, a density of at least 83.75% have
rank 0 or 1. -/
@[category research solved, AMS 11 14]
theorem _08375_le_density_rank_zero_one : 0.8375 ‚â§ atTop.liminf
    fun H ‚Ü¶ ({E ‚àà heightLE H | E.rank = 0 ‚à® E.rank = 1}.ncard / (heightLE H).ncard : ‚Ñù) := by
  sorry

/-- Theorem 5 of [BS2013]:
when elliptic curves over ‚Ñö are ordered by height, a density of at least 20.62% have rank 0. -/
@[category research solved, AMS 11 14]
theorem _02062_le_density_rank_zero : 0.2062 ‚â§ atTop.liminf
    fun H ‚Ü¶ ({E ‚àà heightLE H | E.rank = 0}.ncard / (heightLE H).ncard : ‚Ñù) := by
  sorry

/-- From [PPVW2016], Section 3.1: "from the mid-1960s to the present,
it seems that most experts conjectured unboundedness." -/
@[category research open, AMS 11 14]
theorem unbounded_rank_conjecture (n : ‚Ñï) : ‚àÉ E : RatEllipticCurve, n ‚â§ E.rank := by
  sorry

/-- From [PPVW2016], Section 8.2:
"Our heuristic predicts (a) All but finitely many E ‚àà ‚Ñ∞ satisfy rk E(‚Ñö) ‚â§ 21".
In other words, there are only finitely many elliptic curves over ‚Ñö (up to isomorphism)
with rank greater than 21.
Notice that this contradicts the previous conjecture. -/
@[category research open, AMS 11 14]
theorem finite_twentyone_lt_finrank : {E : RatEllipticCurve | 21 < E.rank}.Finite := by
  sorry

/-- [PPVW2016] 8.2(b): for 1 ‚â§ r ‚â§ 20, the number of elliptic curves over ‚Ñö with rank `r` and
na√Øve height at most `H` is asymptotically `H ^ ((21 - r) / 24 + o(1))`.
Note: ‚Ñ∞_H in 8.2(b) should be ‚Ñ∞_{‚â§H}, see the statement of Theorem 7.3.3.
When `r = 1`, the exponent is `20 / 24 = 5 / 6`, which agrees with the exponent in
`card_heightLE_div_pow_five_div_six_tensto` and is consistent with
`half_rank_zero_and_half_rank_one`. -/
@[category research open, AMS 11 14]
theorem rank_height_count_asymptotic (r : ‚Ñï) (h‚ÇÅ : 1 ‚â§ r) (h‚ÇÇ : r ‚â§ 20) :
    ‚àÉ f : ‚Ñï ‚Üí ‚Ñù, atTop.Tendsto f (ùìù 0) ‚àß
      ‚àÄ H : ‚Ñï, 1 < H ‚Üí {E ‚àà heightLE H | r ‚â§ E.rank}.ncard = (H : ‚Ñù) ^ ((21 - r) / 24 + f H) := by
  sorry

/-- [PPVW2016] 8.2(c): the number of elliptic curves over ‚Ñö with rank ‚â• 21 and na√Øve height
at most `H` is asymptotically at most `H ^ o(1)`. -/
@[category research open, AMS 11 14]
theorem twentyone_le_rank_height_count_asymptotic :
    ‚àÉ f : ‚Ñï ‚Üí ‚Ñù, atTop.Tendsto f (ùìù 0) ‚àß
      ‚àÄ H : ‚Ñï, 1 < H ‚Üí {E ‚àà heightLE H | 21 ‚â§ E.rank}.ncard ‚â§ (H : ‚Ñù) ^ f H := by
  sorry

end RatEllipticCurve

namespace WeierstrassCurve

/-! See https://en.wikipedia.org/wiki/Rank_of_an_elliptic_curve#Largest_known_ranks -/

/-- The elliptic curve over ‚Ñö of rank at least 29 found by Elkies and Klagsbrun in 2024.
It has rank exactly 29 assuming the generalized Riemann hypothesis. -/
def elkiesKlagsbrun29 : WeierstrassCurve ‚Ñö where
  a‚ÇÅ := 1
  a‚ÇÇ := 0
  a‚ÇÉ := 0
  a‚ÇÑ := -27006183241630922218434652145297453784768054621836357954737385
  a‚ÇÜ := 55258058551342376475736699591118191821521067032535079608372404779149413277716173425636721497

/-- See https://mathoverflow.net/a/478050. -/
@[category test, AMS 11 14]
theorem Œî_elkiesKlagsbrun29 : elkiesKlagsbrun29.Œî =
    -2 ^ 19 * 3 ^ 7 * 5 ^ 7 * 7 ^ 4 * 11 ^ 5 * 13 ^ 3 * 17 ^ 4 * 31 ^ 3 * 41 ^ 2 * 43 ^ 2 * 61 ^ 2 *
    233 * 241 ^ 2 * 4139 * 678146849364709860535420504397393 *
    159788990966780131363155786084695062643236502969 *
    4402149008473369392540402625019227412319473055901 := by
  rw [elkiesKlagsbrun29, Œî, b‚ÇÇ, b‚ÇÑ, b‚ÇÜ, b‚Çà]; norm_num

@[category test, AMS 11 14]
instance : elkiesKlagsbrun29.IsElliptic where
  isUnit := by rw [Œî_elkiesKlagsbrun29]; norm_num

@[category research solved, AMS 11 14]
theorem twentynine_le_rank_elkiesKlagsbrun29 : 29 ‚â§ finrank ‚Ñ§ elkiesKlagsbrun29‚üÆ‚Ñö‚üØ := by
  sorry

@[category research open, AMS 11 14]
theorem rank_elkiesKlagsbrun29 : finrank ‚Ñ§ elkiesKlagsbrun29‚üÆ‚Ñö‚üØ = 29 := by
  sorry

/-- The elliptic curve over ‚Ñö of rank at least 28 found by Elkies in 2006.
It has rank exactly 28 assuming the generalized Riemann hypothesis. -/
def elkies28 : WeierstrassCurve ‚Ñö where
  a‚ÇÅ := 1
  a‚ÇÇ := -1
  a‚ÇÉ := 1
  a‚ÇÑ := -20067762415575526585033208209338542750930230312178956502
  a‚ÇÜ := 34481611795030556467032985690390720374855944359319180361266008296291939448732243429

/-- See https://mathoverflow.net/a/478050. -/
@[category test, AMS 11 14]
theorem Œî_elkies28 : elkies28.Œî =
    2 ^ 15 * 3 ^ 6 * 5 ^ 6 * 7 ^ 4 * 11 ^ 2 * 13 ^ 4 * 17 ^ 5 * 19 ^ 3 *
    48463 * 20650099 * 315574902691581877528345013999136728634663121 *
    376018840263193489397987439236873583997122096511452343225772113000611087671413 := by
  rw [elkies28, Œî, b‚ÇÇ, b‚ÇÑ, b‚ÇÜ, b‚Çà]; norm_num

@[category test, AMS 11 14]
instance : elkies28.IsElliptic where
  isUnit := by rw [Œî_elkies28]; norm_num

@[category research solved, AMS 11 14]
theorem twentyeight_le_rank_elkies28 : 28 ‚â§ finrank ‚Ñ§ elkies28‚üÆ‚Ñö‚üØ := by
  sorry

@[category research open, AMS 11 14]
theorem rank_elkies28 : finrank ‚Ñ§ elkies28‚üÆ‚Ñö‚üØ = 28 := by
  sorry

-- TODO: compute the rank of some rank 0 / 1 curve.

end WeierstrassCurve
